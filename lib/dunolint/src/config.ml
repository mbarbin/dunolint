(*********************************************************************************)
(*  Dunolint - A tool to lint and help manage files in dune projects             *)
(*  Copyright (C) 2024-2025 Mathieu Barbin <mathieu.barbin@gmail.com>            *)
(*                                                                               *)
(*  This file is part of Dunolint.                                               *)
(*                                                                               *)
(*  Dunolint is free software; you can redistribute it and/or modify it          *)
(*  under the terms of the GNU Lesser General Public License as published by     *)
(*  the Free Software Foundation either version 3 of the License, or any later   *)
(*  version, with the LGPL-3.0 Linking Exception.                                *)
(*                                                                               *)
(*  Dunolint is distributed in the hope that it will be useful, but WITHOUT      *)
(*  ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or        *)
(*  FITNESS FOR A PARTICULAR PURPOSE. See the GNU Lesser General Public License  *)
(*  and the file `NOTICE.md` at the root of this repository for more details.    *)
(*                                                                               *)
(*  You should have received a copy of the GNU Lesser General Public License     *)
(*  and the LGPL-3.0 Linking Exception along with this library. If not, see      *)
(*  <http://www.gnu.org/licenses/> and <https://spdx.org>, respectively.         *)
(*********************************************************************************)

open! Import
module V0 = Config_v0

module T = struct
  [@@@coverage off]

  type t = [ `v0 of V0.t ] [@@deriving_inline compare, equal]

  let compare =
    (fun a__001_ ->
       fun b__002_ ->
       if Stdlib.( == ) a__001_ b__002_
       then 0
       else (
         match a__001_, b__002_ with
         | `v0 _left__003_, `v0 _right__004_ -> V0.compare _left__003_ _right__004_)
     : t -> t -> int)
  ;;

  let equal =
    (fun a__005_ ->
       fun b__006_ ->
       if Stdlib.( == ) a__005_ b__006_
       then true
       else (
         match a__005_, b__006_ with
         | `v0 _left__007_, `v0 _right__008_ -> V0.equal _left__007_ _right__008_)
     : t -> t -> bool)
  ;;

  [@@@deriving.end]
end

include T

(* When breaking changes are introduced, we'd mint new versions such as:

   {[
     module V0 = Config_v0
     module V1 = Config_v1
     module V2 = Config_v2

     type t =
       [ `v0 of V0.t
       | `v1 of V1.t
       | `v2 of V2.t
       ]
   ]}

   The evaluation engine is responsible for handing the different versions,
   accessing them using [Private.view]. *)

module type To_sexp = sig
  type t

  val sexp_of_t : t -> Sexp.t
end

let to_versioned_sexp_internal
      (type a)
      (module T : To_sexp with type t = a)
      (a : a)
      ~version
  : Sexp.t
  =
  List [ List [ Atom "version"; Atom version ]; T.sexp_of_t a ]
;;

let sexp_of_t (t : t) : Sexp.t =
  match t with
  | `v0 v0 -> to_versioned_sexp_internal (module V0) v0 ~version:"0"
;;

let t_of_sexp (sexp : Sexp.t) =
  match sexp with
  | List [ List [ Atom "version"; (Atom version as version_sexp) ]; config ] ->
    (match version with
     | "0" -> `v0 (V0.t_of_sexp config)
     | _ ->
       raise
         (Sexp.Of_sexp_error
            ( Failure (Printf.sprintf "Unsupported dunolint config version [%s]." version)
            , version_sexp )))
  | _ -> `v0 (V0.t_of_sexp sexp)
;;

let v0 t = `v0 t

let to_file_contents t ~generated_by =
  String.concat
    ~sep:"\n"
    [ Printf.sprintf ";; This file is generated by [%s]. Do not edit!" generated_by
    ; Sexp.to_string_hum (sexp_of_t t)
    ]
;;

module Private = struct
  let view t = t
end

module Skip_subtree = Config_v0.Skip_subtree
module Rule = Config_v0.Rule
module Std = Config_v0.Std

let skip_subtree (t : t) =
  match t with
  | `v0 v0 -> V0.skip_subtree v0
;;

let rules (t : t) =
  match t with
  | `v0 v0 -> V0.rules v0
;;

let create ?skip_subtree ?rules () = `v0 (V0.create ?skip_subtree ?rules ())
