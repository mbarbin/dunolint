(*********************************************************************************)
(*  Dunolint - A tool to lint and help manage files in dune projects             *)
(*  Copyright (C) 2024-2025 Mathieu Barbin <mathieu.barbin@gmail.com>            *)
(*                                                                               *)
(*  This file is part of Dunolint.                                               *)
(*                                                                               *)
(*  Dunolint is free software; you can redistribute it and/or modify it          *)
(*  under the terms of the GNU Lesser General Public License as published by     *)
(*  the Free Software Foundation either version 3 of the License, or any later   *)
(*  version, with the LGPL-3.0 Linking Exception.                                *)
(*                                                                               *)
(*  Dunolint is distributed in the hope that it will be useful, but WITHOUT      *)
(*  ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or        *)
(*  FITNESS FOR A PARTICULAR PURPOSE. See the GNU Lesser General Public License  *)
(*  and the file `NOTICE.md` at the root of this repository for more details.    *)
(*                                                                               *)
(*  You should have received a copy of the GNU Lesser General Public License     *)
(*  and the LGPL-3.0 Linking Exception along with this library. If not, see      *)
(*  <http://www.gnu.org/licenses/> and <https://spdx.org>, respectively.         *)
(*********************************************************************************)

open! Import
module V1 = Config_v1

type t = [ `v1 of V1.t ]

let equal (a : t) (b : t) =
  if Stdlib.( == ) a b
  then true
  else (
    match a, b with
    | `v1 va, `v1 vb -> V1.equal va vb)
;;

(* When breaking changes are introduced, we'd mint new versions such as:

   {[
     module V1 = Config_v1
     module V2 = Config_v2

     type t =
       [ `v1 of V1.t
       | `v2 of V2.t
       ]
   ]}

   The evaluation engine is responsible for handing the different versions,
   accessing them using [Private.view]. *)

let to_stanzas : t -> Sexp.t list = function
  | `v1 v1 -> List [ Atom "lang"; Atom "dunolint"; Atom "1.0" ] :: V1.to_stanzas v1
;;

let of_stanzas : Sexp.t list -> t = function
  | [] -> `v1 (V1.create [])
  | List [ Atom "lang"; Atom "dunolint"; (Atom version as version_sexp) ] :: sexps ->
    (match version with
     | "1.0" -> `v1 (V1.of_stanzas sexps)
     | _ ->
       raise
         (Sexp.Of_sexp_error
            ( Failure (Printf.sprintf "Unsupported dunolint config version [%s]." version)
            , version_sexp )))
  | sexp :: _ ->
    raise
      (Sexp.Of_sexp_error
         (Failure "Dunolint config expected to start with (lang dunolint VERSION).", sexp))
;;

let sexp_of_t (t : t) : Sexp.t = List (Atom "stanzas" :: to_stanzas t)
let v1 t = `v1 t

let to_file_contents t ~generated_by =
  String.concat
    ~sep:"\n"
    (Printf.sprintf ";; This file is generated by [%s]. Do not edit!" generated_by
     :: List.mapi (to_stanzas t) ~f:(fun i sexp ->
       (if i > 0 then "\n" else "") ^ Sexp.to_string_hum sexp))
;;

module Std = Edsl_std

module Private = struct
  let view t = t
end

module Rule = Config_v1.Rule

let create ?(rules = []) () = `v1 (V1.create (List.map rules ~f:(fun rule -> `rule rule)))
